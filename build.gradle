plugins {
    id "java"
    id "com.github.johnrengelman.shadow" version "8.1.1"
    id "xyz.jpenilla.run-paper" version "2.0.1"
}

repositories {
    mavenCentral()
    maven {
        url "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        url "https://repo.extendedclip.com/content/repositories/placeholderapi/"
    }
    maven {
        url "https://jitpack.io/"
    }
}

dependencies {
    compileOnly "io.papermc.paper:paper-api:1.19.4-R0.1-SNAPSHOT"
    compileOnly "me.clip:placeholderapi:2.11.1"

    //noinspection GradlePackageUpdate
    compileOnly "io.netty:netty-all:4.1.87.Final"
    compileOnly "com.mojang:authlib:3.4.40"
    compileOnly "com.mojang:datafixerupper:6.0.8"

    implementation "org.apache.commons:commons-lang3:3.12.0"
    implementation "commons-io:commons-io:2.11.0"
    implementation "com.google.code.gson:gson:2.10.1"
    implementation "org.bstats:bstats-bukkit:3.0.2"
    implementation "com.github.robertlit:SpigotResourcesAPI:2.0"
}

group "lol.pyr"
version "1.0.4"

compileJava {
    options.release.set(17)
}

shadowJar {
    archiveClassifier.set ""
    relocate "org.bstats", "lol.pyr.znpcsplus.lib.bstats"
    relocate "org.apache.commons", "lol.pyr.znpcsplus.lib.commons"
    relocate "com.google.gson", "lol.pyr.znpcsplus.lib.gson"
    relocate "me.robertlit.spigotresources", "lol.pyr.znpcsplus.lib.spigotapi"
    minimize()
}

processResources {
    expand("version": version)
}

runServer {
    minecraftVersion "1.19.4"
}

tasks.assemble.dependsOn shadowJar
tasks.compileJava.dependsOn clean
